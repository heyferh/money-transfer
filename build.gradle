plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'jacoco'
    id "net.ltgt.apt" version "0.15"
}

group = 'com.heyferh'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "com.heyferh.test.Main"

sourceSets {
    main {
        output.resourcesDir = output.classesDirs.singleFile
    }
}

idea {
    module {
        sourceDirs += file("$buildDir/generated/source/apt/main")
        testSourceDirs += file("$buildDir/generated/source/apt/test")
    }
}

task fatJar(type: Jar) {
    classifier = 'with-dependencies'
    manifest {
        attributes(
                "Main-Class": "$mainClassName"
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = true
    }
}

dependencies {
    compile 'org.hibernate:hibernate-core:5.2.9.Final'
    compile 'org.hibernate:hibernate-c3p0:5.2.9.Final'
    compile 'com.h2database:h2:1.4.197'
    compile 'com.sparkjava:spark-core:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.10'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.10'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'com.google.dagger:dagger:2.6'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.6'
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}